' Gambas class file

' Gambas class selector

Public connection_socket As Socket
Public line_type As String[]
Public line_path As String[]
Public line_host As String[]
Public line_port As Integer[]
Public line_prompt As String[]
Public back_list As String[]
Public back_list_ports As Integer[]
Public forward_list As String[]
Public forward_list_ports As Integer[]
Public current_address As String
Public current_port As Integer
Public bookmarks As String
Public bookmark_buttons As Menu[]

Public Sub add_back()
  If current_address <> "" Then
    back_list.Add(current_address)
    back_list_ports.Add(current_port)
    btnBack.Enabled = True
  Endif
End


Public Sub visit_site(domain As String, port As Integer)
  Dim sBuf As String
  
  btnAddBookmark.Enabled = False
  btnRefresh.enabled = False
  current_address = domain
  current_port = port
  txtPageContent.Clear
  
  If Len(domain) >= 9 Then
    If Lower(Mid(domain, 1, 9)) = "gopher://" Then
      domain = Mid(domain, 10, domain.Len - 9)
    Endif
  Else If Len(domain) = 0 Then
    Return
  Endif
  
  lblProgress.text = "Connecting to " & domain & "..."
  
  Dim tokens As String[]
  tokens = Split(domain, "/")
  domain = tokens[0]
  
  Dim selector As String
  selector = "/"
  If tokens.Length > 1 Then
    tokens.Delete(0)
    selector = "/" & tokens.join("/")
  End If
  
  txtDomain.text = "gopher://" & domain & selector
  
  Dim max_tries As Integer
  max_tries = 100
  
  connection_socket = New Socket
  connection_socket.Connect(domain, port)
  connection_socket.Blocking = False

  Do While (connection_socket.Status <> Net.Connected) And (connection_socket.Status > 0)
    Wait 0.1
    max_tries = max_tries - 1
    If max_tries = 0 Then
      connection_socket.Close
      Break
    Endif
  Loop

  If connection_socket.Status <= 0 Then
    lblProgress.text = "Error connecting to " & domain & "."
    Return
  End If
  
  lblProgress.text = "Requesting " & domain & "..."
  lblProgress.Refresh
  Wait 0.0

  sBuf = selector & "\n"
  Write #connection_socket, sBuf, Len(sBuf)

  Do While Lof(connection_socket) = 0
    Wait 0.1
  Loop
  
  sBuf = ""
  Dim download_buffer As String
  Dim byteCount As Integer, bytesToGet As Integer
  While True
    bytesToGet = 256
    byteCount = bytesToGet + byteCount
    lblProgress.text = "Fetching " & domain & " (" & byteCount & " bytes)..."
    Wait 0.0
    Try Read #connection_socket, download_buffer, bytesToGet
    If Error Or download_buffer.Len = 0 Then
      lblProgress.text = "Fetched " & domain & " (" & byteCount & " bytes)..."
      Wait 0.0
      Break
    Endif
    sBuf = sBuf & download_buffer
    If connection_socket.status < 0 Then
      lblProgress.text = "Error loading " & domain & "..."
      Return
    End If
  Wend
  
  Dim page_lines As String[]
  page_lines = Split(sBuf, "\n")
  line_type = New String[page_lines.length]
  line_path = New String[page_lines.length]
  line_host = New String[page_lines.length]
  line_port = New Integer[page_lines.length]
  line_prompt = New String[page_lines.length]
  Dim line_tokens As String[]
  Dim value As Variant
  Dim i As Integer
  Dim line_type_char As String 
  Dim line_contents, prefix As String
  Dim page_contents As String
  page_contents = ""
  For i = 0 To page_lines.Length - 1
    lblProgress.text = "Processing line " & (i + 1) & " / " & page_lines.Length & "..."
    Wait 0.0
    If Trim(page_lines[i]) = "." Then
      Break
    End If
    line_tokens = Split(Trim(page_lines[i]), "\t")
    If line_tokens.Length < 2 Then
      page_contents &= page_lines[i] & "\n"
      line_path[i] = ""
      line_host[i] = ""
      line_port[i] = 0
      line_type[i] = "i"
      line_prompt[i] = ""
    Else
      If line_tokens.length > 1 Then line_path[i] = line_tokens[1] Else line_path[i] = ""
      If line_tokens.length > 2 Then line_host[i] = line_tokens[2] Else line_host[i] = ""
      If line_tokens.length > 3 Then
        value = Val(line_tokens[3])
        If IsNull(value) Then line_port[i] = 0 Else line_port[i] = value
      Else
        line_port[i] = 70
      End If
      If line_tokens[0].Len < 1 Then
        page_contents &= "\n"
        line_type[i] = "i"
      Else
        line_contents = Mid(line_tokens[0], 2, line_tokens[0].Len)
        line_type_char = line_tokens[0][0]
        line_type[i] = line_type_char
        Select Case line_type_char
          Case "i"
            prefix = "          "
          Case "0"
            prefix = "(Text)    "
          Case "1"
            prefix = "(Submenu) "
          Case "2"
            prefix = "(Error)   "
          Case "3"
            prefix = "(CCSO)    "
          Case "4"
            prefix = "(BinHex)  "
          Case "5"
            prefix = "(DOS)     "
          Case "6"
            prefix = "(Uuenc.)  "
          Case "7"
            prefix = "(Entry)   "
          Case "8"
            prefix = "(Telnet)  "
          Case "9"
            prefix = "(Binary)  "
          Case "+"
            prefix = "(Mirror)  "
          Case "g"
            prefix = "(GIF)     "
          Case "I"
            prefix = "(Image)   "
          Case "T"
            prefix = "(Tel3270) "
          Case "d"
            prefix = "(DOC/PDF) "
          Case "h"
            prefix = "(HTML)    "
          Case "s"
            prefix = "(Audio)   "
          Case Else
            prefix = line_type_char
        End Select
        If line_type_char <> "7" Then
          line_prompt[i] = ""
        Else
          line_prompt[i] = line_contents
        Endif
          page_contents &= prefix & line_contents & "\n"
      Endif
    Endif
  Next
  
  txtPageContent.Text = page_contents
  
  lblProgress.text = "Loaded " & domain & " (" & byteCount & " bytes, " & page_lines.Length & " lines)."
  
  btnAddBookmark.Enabled = True  
  btnRefresh.enabled = True
  txtPageContent.line = 0
  txtPageContent.Select(0, 0)

  Close #connection_socket
  ' Catch
  '   lblProgress.text = "Error during processing of " & domain & "..."
End


Public Sub btnGo_Click()
  Dim domain As String
  domain = Trim(txtDomain.text)
  If domain <> "" Then
    add_back()
    forward_list.Clear
    btnForward.enabled = False
    visit_site(domain, 70)
  End If
End

Public Sub txtDomain_KeyPress()
  If Key.Code = key.Return Or Key.code = Key.Enter Then
    btnGo_Click()
  Endif
End

Public Sub txtPageContent_DblClick()
  Dim lineNumber As Integer
  Dim pressed_line_type As String
  lineNumber = txtPageContent.Line
  pressed_line_type = line_type[lineNumber]
  Select Case pressed_line_type
    Case "1", "h", "0"
      add_back()
      forward_list.Clear
      btnForward.enabled = False
      visit_site(line_host[lineNumber] & line_path[lineNumber], line_port[lineNumber])
    Case "7"
      Dim text_entered As String
      text_entered = InputBox(line_prompt[lineNumber], "OpenNapkin Text Entry", "")
      add_back()
      forward_list.Clear
      btnForward.enabled = False
      visit_site(line_host[lineNumber] & line_path[lineNumber] & "\t" & text_entered, line_port[lineNumber])
    Case "s", "4", "5", "6", "9", "g", "I", "d"
      Dim filename As String, fullpath As String, path_parts As String[]
      fullpath = line_host[lineNumber] & line_path[lineNumber]
      path_parts = Split(fullpath, "/")
      If path_parts.Length > 0 Then
        filename = path_parts[path_parts.length - 1]
        Dialog.path = filename
        Dialog.Filter = ["*", "All Files"]
        If Dialog.SaveFile() Then Return
          download_file(Dialog.path, line_host[lineNumber], line_path[lineNumber], line_port[lineNumber])
        End If
  End Select
  Catch
End

Public Sub download_file(path As String, host As String, selector As String, port As Integer)
  Dim sBuf As String
  
  lblProgress.text = "Connecting to " & host & "..."
  
  Dim max_tries As Integer
  max_tries = 100
  
  connection_socket = New Socket
  connection_socket.Connect(host, port)

  Do While (connection_socket.Status <> Net.Connected) And (connection_socket.Status > 0)
    Wait 0.1
    max_tries = max_tries - 1
    If max_tries = 0 Then
      connection_socket.Close
      Break
    Endif
  Loop

  If connection_socket.Status <= 0 Then
    lblProgress.text = "Error connecting to " & host & "."
    Return
  End If
  
  lblProgress.text = "Requesting " & path & "..."
  lblProgress.Refresh
  Wait 0.0

  sBuf = selector & "\n"
  Write #connection_socket, sBuf, Len(sBuf)

  Do While Lof(connection_socket) = 0
    Wait 0.1
  Loop
  
  Dim download_buffer As String
  Dim destinationFile As File
  Dim byteCount As Integer, bytesToGet As Integer
  destinationFile = Open path For Write Create
  connection_socket.Blocking = True
  While Not Eof(connection_socket)
    bytesToGet = Lof(connection_socket)
    byteCount = bytesToGet + byteCount
    lblProgress.text = "Fetching " & path & " (" & byteCount & " bytes)..."
    lblProgress.Refresh
    Wait 0.0
    Read #connection_socket, download_buffer, bytesToGet
    Write #destinationFile, download_buffer, download_buffer.Len
  Wend
  destinationFile.Close
  'File.Save(path, download_buffer)
  
  lblProgress.text = "Downloaded " & path & "."
  Close #connection_socket
  'Catch
   ' lblProgress.text = "Error dowloading " & path & "..."
End



Public Sub Form_Open()
  Me.width = 800
  Me.height = 600
  back_list = New String[0]
  forward_list = New String[0]
  back_list_ports = New Integer[0]
  forward_list_ports = New Integer[0]
  bookmark_buttons = New Menu[0]
  current_address = ""
  current_port = 70
  ' Load bookmarks
  load_bookmarks
End

Public Sub load_bookmarks()
  Dim bookmark_list As String[], i As Integer
  If bookmark_buttons.Length > 0 Then
    Dim bm As Menu
    For Each bm In bookmark_buttons
      bm.Delete()
    Next
    bookmark_buttons.clear
  Endif
  bookmarks = File.Load("~/.opennapkin_bookmarks")
  bookmark_list = Split(bookmarks, "\n")
  For i = 0 To bookmark_list.Length - 1
    Dim bookmark_parts As String[]
    bookmark_parts = Split(Trim(bookmark_list[i]), " ")
    If bookmark_parts.length < 2 Then
      Continue
    Endif
    If bookmark_parts[0] = "*" Then
      Continue
    Endif
    add_bookmark_button(Trim(bookmark_list[i]))
  Next
  Catch
    'No bookmark file found
    File.Save("~/.opennapkin_bookmarks", "* OpenNapkin Bookmarks File\n* Line format:\n* <port> <gopher address>")
End

Private Sub add_bookmark_button(text As String)
  If text = "" Then
    Return
  Endif
  Dim bookmark_button As Menu
  bookmark_button = New Menu(menBookmarks, False) As "bookmarkedItem"
  Dim bookmark_parts As String[], port As String, address As String
  bookmark_parts = Split(text, " ")
  port = bookmark_parts[0]
  bookmark_parts.Delete(0)
  address = bookmark_parts.Join(" ")
  bookmark_button.Text = address
  bookmark_button.Tag = port
  bookmark_buttons.Add(bookmark_button)
End

Public Sub bookmarkedItem_Click()
  add_back()
  forward_list.Clear
  btnForward.enabled = False
  visit_site(Last.text, Val(Last.tag))
End


Public Sub add_bookmark()
  If current_address <> "" Then
    bookmarks = bookmarks & "\n" & current_port & " " & current_address
    File.Save("~/.opennapkin_bookmarks", bookmarks)
    add_bookmark_button(current_port & " " & current_address)
  Endif
End


Public Sub btnRefresh_Click()
  visit_site(current_address, current_port)
End

Public Sub btnBack_Click()
  If back_list.Length > 0 Then
    Dim domain As String, port As Integer
    domain = back_list.Pop()
    port = back_list_ports.Pop()
    If back_list.Length == 0 Then
      btnBack.enabled = False
    Endif
    forward_list.Add(current_address)
    forward_list_ports.Add(current_port)
    btnForward.enabled = True
    visit_site(domain, port)
  Endif
End

Public Sub btnForward_Click()
  If forward_list.Length > 0 Then
    Dim domain As String, port As Integer
    domain = forward_list.Pop()
    port = forward_list_ports.Pop()
    If forward_list_ports.Length == 0 Then
      btnForward.enabled = False
    Endif
    back_list.Add(current_address)
    back_list_ports.Add(current_port)
    btnBack.enabled = True
    visit_site(domain, port)
  Endif
End

Public Sub btnAddBookmark_Click()
  add_bookmark
End

Public Sub menQuit_Click()
  Quit
End


Public Sub menAbout_Click()
  frmAbout.ShowModal
End

Public Sub btnSavePage_Click()
  Dialog.Filter = ["*.txt", "Text Files"]
  If Dialog.SaveFile() Then Return
  File.Save(Dialog.Path, txtPageContent.Text)
Catch
  Message.Info(Error.Text)
End

Public Sub btnBookmarkEditor_Click()
  frmBookmarkEditor.ShowModal
End

Public Sub menBookmarkThis_Click()
  add_bookmark
End
